#! /bin/csh -f
#
#   Archive production netCDF data files for 2000-107 Sea Salinity
#    using the new ATD directory path
#
#   Must be run from a project's Production subdirectory
#
#  File adapted from file /jnet/local/proj/186/Production/atdarch.186
#    by Ron Ruth   13 October 2K
#
#   To find executable (non-comment) lines (from within `vi'), type:
#       /^[^#]
#
#  `vi' :map commands for annotating job completion:
#  :map g eea 
#  :map v AOK @ 
#
#
#  Check login (only "dmg" login allowed to run this script)
set user = `whoami`
if ($user == "dmg") then
   set dmg = "yes"
else
#  echo "${0}:  Only the 'dmg' login is allowed to run this script.  Quitting."
#  exit
   set dmg = "no"
endif
#
#  Get setup information for this project
@ plat = `pwd | sed -e "s/\(.*\)\([0-9][0-9][0-9]\)\(.*\)/\2/g"`
@ plat /= 100
if ($plat == 1) set PLATFORM = N130AR
if ($plat == 8) set PLATFORM = N308D
set YEAR = `grep CY proj.info | sed -e "s/CY=//"`
set PROJ = `cat ../ProjectName`
chk:
@ spaces = `echo $PROJ | grep -c ' '`
if ($spaces > 0) then
  set PROJ = `echo $PROJ | sed -e "s/ /_/"`
  goto chk
endif
#
#  set extent for Production NIMBUS netCDF file
set EXT = PNI.nc
#
# Set correct turbulence type
set TYPE = LRT
#set TYPE = HRT
#
#  set source directory
#set SDIR = $TEMP
set SDIR = $PROD_DATA
#set SDIR = $DATA_DIR
#
#
#  1st wave
set SFILES = ( \
107rf01.cdf \
107rf02.cdf \
107rf03.cdf \
107rf04.cdf \
107rf05.cdf \
107rf06.cdf \
107rf07.cdf \
)
set DFILES = ( \
RF01 \
RF02 \
RF03 \
RF04 \
RF05 \
RF06 \
RF07 \
)
#
#  2nd wave
#set SFILES = ( \
#)
#set DFILES = ( \
#)
#
#  3rd wave
#set SFILES = ( \
#)
#set DFILES = ( \
#)
#
#   th wave
#set SFILES = ( \
#)
#set DFILES = ( \
#)
#
#
#  Get started:
date
if ($#SFILES != $#DFILES) then
  echo " Number of source and destination file names don't match.  Quitting"
  exit 1
endif
#
set todo = $#SFILES
set num = 1
while ($num <= $todo)
  set SFILE = $SFILES[$num]
  set DFILE = $DFILES[$num]
  if (-r $SDIR/$SFILE) then
#
# Compose extended bitfile name
#  get flight date
    set line = `ncdump -h $SDIR/$SFILE | grep FlightDate`
    if ($status != 0) then
      echo Could not find FlightDate in netCDF file $file
      goto quit
    endif
#   Get rid of double quotes
    set line = `echo $line  | sed -e 's/\"//g'`
#   Get rid of time zone in date field, if extant
    set line = `echo $line | sed -e 's/UTC//'`
#   Test for styles #1 & 2
    if ($#line == 4) then
      set incnt1 = `echo $line[3] | wc -c`
      if ($incnt1 == 11) then
#   Style #1 (mm/dd/yyyy)
        set fltdate = `echo $line[3]`
      else if ($incnt1 == 10) then
#   Style #2 (" m/dd/yyyy)
        set fltdate = 0$line[3]
      else
        echo "Bug in script $0 decoding Case #1 flight date.  Notify author."
        goto quit
      endif
    else if ($#line == 5) then
      set incnt1 = `echo $line[3] | wc -c`
      set incnt2 = `echo $line[4] | wc -c`
      if ($incnt1 == 3 && $incnt2 == 7) then
#   Style #3, ( m/ d/yyyy)
        set fltdate = `echo 0$line[3]0$line[4]`
#   Style #4 (mm/ d/yyyy)
      else if ($incnt1 == 4 && $incnt2 == 7) then
        set fltdate = `echo $line[3]0$line[4]`
      else
#   Unknown date style
        echo "Bug in script $0 decoding Case #1 flight date.  Notify author."
        goto quit
      endif
    else
#   Unknown date style
      echo "Bug in script $0 decoding Case #1 flight date.  Notify author."
      goto quit
    endif
    set DATE = `echo $fltdate | awk -F/ '{print $3$1$2}'`
#
#  Get time interval
    set tline = `ncdump -h $SDIR/$SFILE | grep TimeInterval`
    if ($status != 0) then
      echo Could not find TimeInterval in netCDF file $file
      goto quit
    endif
    shift tline
    shift tline
    @ pass = 1
newtime1:
    echo $tline | grep , >& /dev/null
#   Check for multiple time intervals
    if ($status == 0) then
      set multtime = "yes"
    else
      set multtime = "no"
    endif
#  Set time interval
    set flttime = `echo $tline[1] | sed -e 's/\"//g'`
    if ($status != 0) then
      echo Could not decode TimeInterval in netCDF file $file
      goto quit
    endif
    set flttime = `echo $flttime | sed -e 's/-/\ /'`
    if ($status != 0) then
      echo Could not decode TimeInterval in netCDF file $file
      goto quit
    endif
    if ($multtime == "yes") then
      set flttime = `echo $flttime | sed -e 's/\,//'`
      if ($status != 0) then
        echo Could not decode multiple TimeInterval in netCDF file $file
        goto quit
      endif
    endif
    set ST = `echo $flttime[1] | awk -F: '{print $1$2$3}'`
    set ET = `echo $flttime[2] | awk -F: '{print $1$2$3}'`
#
#  Archive the file (if the "dmg" login)
    if ($dmg == "yes") then
      echo \
      nrnet msput $SDIR/$SFILE l df=tr r \
        flnm=/ATD/DATA/$YEAR/$PROJ/$PLATFORM/$TYPE/$DFILE.$DATE.${ST}_$ET.$EXT \
        rtpd=32767 name=RAF proj=41113009 w=RAFDMG
    else
      echo msput $SDIR/$SFILE l df=tr r \
        flnm=/ATD/DATA/$YEAR/$PROJ/$PLATFORM/$TYPE/$DFILE.$DATE.${ST}_$ET.$EXT \
        rtpd=32767 name=RAF proj=41113009 w=RAFDMG
    endif
    @ num ++
  else
    echo " Cannot access file $SDIR/$SFILE"
    exit 1
  endif
end
#
exit
#
#  MSS path for all bitfiles listed below:  /ATD/DATA/2000/Sea_Salinity/N130AR/
#
#  1st wave
#  Job(s) submitted on Mon Oct 30 15:47:14 MST 2000
# Sequence number = LN3425 -- LRT/RF01.20000814.190621_230345.PNI.nc -- OK @ 30 Oct 2000 15:47:50
# Sequence number = LN3456 -- LRT/RF02.20000817.002616_065501.PNI.nc -- OK @ 30 Oct 2000 15:48:06
# Sequence number = LN3490 -- LRT/RF03.20000818.005729_062737.PNI.nc -- OK @ 30 Oct 2000 15:48:03
# Sequence number = LN3524 -- LRT/RF04.20000819.005818_054421.PNI.nc -- OK @ 30 Oct 2000 15:47:58
# Sequence number = LN3557 -- LRT/RF05.20000821.005914_060025.PNI.nc -- OK @ 30 Oct 2000 15:51:57
# Sequence number = LN3590 -- LRT/RF06.20000822.003756_073340.PNI.nc -- OK @ 30 Oct 2000 15:52:01
# Sequence number = LN3621 -- LRT/RF07.20000825.012719_060833.PNI.nc -- OK @ 30 Oct 2000 15:51:54
#
#  2nd wave
#  Job(s) submitted on 
#
#  3rd wave
#  Job(s) submitted on 
#
#   th wave
#  Job(s) submitted on 
#
#  Job(s) submitted on 
#
