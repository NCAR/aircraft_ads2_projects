#! /bin/csh -f
#
#  Archive raw ADS data tapes for yyyy-ppp name (PI)
#
#  Revised by RLR  (adapt to new MIGS "msimp" verb)                17 Mar 1999
#  Revised by RLR  (add noglob, "start" code)                       3 May 1999
#  Revised by RLR  (# TAPES and # FLIGHTS must match)              14 Jul 1999
#  Revised by RLR  (add vi commands to ease flt and tape # lists)  30 Nov 1999
#  Revised by RLR  (# SKIPS and # RECS must match, too,
#                                               if greater than 1)  02 Dec 1999
#  Revised by RLR (change the way $PROJ is determined)              20 Mar 2000
#
#  Note:  For the tape images to be archived correctly, run this script:
#         1.  under the "dmg" login ONLY;
#         2.  from the project's `Production/archive' subdirectory.
#
#   To find executable (non-comment) lines (from within `vi'), type:
#       /^[^#]
#
#   Using 'vi' to edit lines extracted from a "tapes.ppp" file:
#     first extract only tape and flight numbers
#        :'a,'bs/\(............\)\(.*\)/\1
#     copy the list to second section
#        :'a,'bco.
#     produce the tape number list
#        :'a,'bs/.......$/ \\
#     produce the flight number list
#        :'c,'ds/\(.......\)\(.*\)/\2 \\
#
#   `vi' :map commands for annotating job completion:
#   :map g eea 
#   :map v AOK @ 
#
#  Check login (only "dmg" login allowed to run this script)
set user = `whoami`
if ($user != "dmg") then
  echo "${0}:  Only the 'dmg' login is allowed to run this script.  Quitting."
  exit
endif
#
set noglob
#
#  Get initialization info
set pfile = ../proj.info
set YEAR = `grep FY $pfile | sed "s/FY=//"`
set PROJ = `pwd | sed -e "s/\(.*\)\([0-9][0-9][0-9]\)\(.*\)/\2/g"`
#
#set TYPE = "9track ermx=99 den=1600"
#set EOT  = eotp-ignore
#
#set TYPE = 8200
set TYPE = 8500
set EOT  = eotp-end
#
#set SKIPS   = ^B*0
#set SKIPS   = ^B*100000
#set SKIPS   = ^B*200000
#
#set RECS    = F*1
#set RECS    = B*100000
#
goto start
#
start:
#
#      set,     segment,     try
set SKIPS   = ^B*0
set RECS    = B*100000
set TAPES   = ( \
Rx \
)
set FLIGHTS = ( \
RFx \
)
#
#  Get started:
date
#
#  Make sure I have the same number of tapes, flights and command arguments
if ($#TAPES != $#FLIGHTS) then
  echo "TAPES and FLIGHTS don't match.  Quitting."
  exit 1
endif
if ($#SKIPS > 1) then
  if ($#SKIPS != $#FLIGHTS) then
    echo "SKIPS and FLIGHTS don't match.  Quitting."
    exit 1
  endif
endif
if ($#RECS > 1) then
  if ($#RECS != $#FLIGHTS) then
    echo "RECS and FLIGHTS don't match.  Quitting."
    exit 1
  endif
endif
#
set todo = $#TAPES
set num = 1
while ($num <= $todo)
  set TAPE = $TAPES[$num]
  set FLIGHT = $FLIGHTS[$num]
  if ($#SKIPS > 1) then
    set SKIP = $SKIPS[$num]
  else
    set SKIP = $SKIPS
  endif
  if ($#RECS > 1) then
    set REC = $RECS[$num]
  else
    set REC = $RECS
  endif
#
#   Discontinued version (replaced by the verb "msimp")
#  nrnet msimport % r df=BI bksz=50000 rcfm=U dvty=$TYPE den=1600 \
#   name=RAF proj=41113009 mvn=CTRAFDMG w=RAFDMG rtpd=4095 disp=KEEP clas=QQ \
#   skpf=0 $SKIP $REC \
#   vlsr=$TAPE  flnm=/RAF/$YEAR/$PROJ/$FLIGHT/$TAPE
#
  echo \
  nrnet msimp % l proj=41113009 sci=9008 r opts="-keep" blk=50000 \
   fmt=U cos=binary rtpd=4095 wpwd=RAFDMG type=$TYPE sub="$SKIP $REC $EOT" \
   vol=$TAPE flnm=/RAF/$YEAR/$PROJ/$FLIGHT/$TAPE
  @ num ++
end
#
# done.
exit
#
#      set,     segment,     try
#  Jobs submitted on 
#
